plugins {
    id 'java-library'
    id 'maven-publish'

    id 'net.minecrell.licenser' version '0.4.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

group = 'me.jamiemansfield'
archivesBaseName = project.name.toLowerCase()
version = '0.1.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
}

license {
    header = file('HEADER.txt')
}

processResources {
    from 'LICENSE.txt'
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://docs.oracle.com/javase/8/docs/api/'
    )
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java

            pom {
                name = project.name
                description = project.description
                packaging = 'jar'
                url = project.url
                inceptionYear = project.inceptionYear

                scm {
                    url = 'https://github.com/jamiemansfield/gson-simple'
                    connection = 'scm:git:https://github.com/jamiemansfield/gson-simple.git'
                    developerConnection = 'scm:git:git@github.com:jamiemansfield/gson-simple.git'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/jamiemansfield/gson-simple/issues'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'jamierocks'
                        name = 'Jamie Mansfield'
                        email = 'jmansfield@cadixdev.org'
                        url = 'https://www.jamiemansfield.me/'
                        timezone = 'Europe/London'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                url = !version.endsWith('-SNAPSHOT') ?
                        'https://oss.sonatype.org/service/local/staging/deploy/maven2/' :
                        'https://oss.sonatype.org/content/repositories/snapshots/'

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    apply plugin: 'signing'
    signing {
        useGpgCmd()
        required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.publish) }
        sign publishing.publications.mavenJava
    }
}
